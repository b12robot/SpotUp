@echo off
title SpotUp
setlocal enabledelayedexpansion
chcp 65001 >nul 2>&1

:: â•”â•â•â•â•â•â•â•â•â•â•â•â•â•—
:: â•‘  Websites  â•‘
:: â•šâ•â•â•â•â•â•â•â•â•â•â•â•â•
::   Spotify: https://www.spotify.com/download | https://loadspot.pages.dev
:: Spicetify: https://github.com/spicetify/spicetify-cli | https://spicetify.app
::     SpotX: https://github.com/SpotX-Official/SpotX | https://github.com/SpotX-Official/SpotX/discussions/60
::    SpotUp: https://github.com/b12robot/SpotUp

:: â•”â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•—
:: â•‘ SpotUp Config â•‘
:: â•šâ•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•
:: HazÄ±r bir Ã¶nayar seÃ§in veya custom seÃ§erek aÅŸaÄŸÄ±dan Ã¶zelleÅŸtirin.
::   install -> Spotify, SpotX ve Spicetifyâ€™Ä± kurar.
:: uninstall -> Spotify, SpotX ve Spicetify'i kaldÄ±rÄ±r.
:: reinstall -> Spotify, SpotX ve Spicetify'i kaldÄ±rÄ±p yeniden kurar.
::    update -> Spicetify'i gÃ¼nceller.
::    custom -> AÅŸaÄŸÄ±da belirlediÄŸiniz seÃ§eneklere gÃ¶re iÅŸlem yapar.
set preset=custom

:: Spotify: Dijital mÃ¼zik ve podcast akÄ±ÅŸ platformu.
:: Spotify'Ä± kaldÄ±rÄ±r. (true/false)
:: (Spotify'Ä± kaldÄ±rmak SpotX'i de kaldÄ±racaktÄ±r.)
set spotify_uninstall=true
:: Spotify'Ä± kurar. (true/false)
set spotify_install=true

:: SpotX: Spotify iÃ§in reklam engelleme ve gÃ¼ncelleme kontrol aracÄ±.
:: SpotX'i kaldÄ±rÄ±r. (true/false)
set spotx_uninstall=true
:: SpotX'i kurar. (true/false)
set spotx_install=true

:: Spicetify: Spotify arayÃ¼zÃ¼nÃ¼ ve iÅŸlevlerini Ã¶zelleÅŸtirme aracÄ±.
:: Spicetify'Ä± kaldÄ±rÄ±r. (true/false)
set spicetify_uninstall=false
:: Spicetify'Ä± kurar. (true/false)
set spicetify_install=false
:: Spicetify'Ä± gÃ¼nceller. (true/false)
set spicetify_update=false

:: â•”â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•—
:: â•‘ SpotX Config â•‘
:: â•šâ•â•â•â•â•â•â•â•â•â•â•â•â•â•â•
:: SpotX'in Spotify gÃ¼ncelleme yÃ¶ntemleri:
:: (YalnÄ±zca SpotX kurulumu sÄ±rasÄ±nda Spotify gÃ¼ncellemesi tespit edilirse geÃ§erlidir.)
:: overwrite -> Mevcut sÃ¼rÃ¼mÃ¼n Ã¼zerine yazarak gÃ¼nceller.
:: reinstall -> Mevcut sÃ¼rÃ¼mÃ¼ kaldÄ±rÄ±p baÅŸtan yÃ¼kler.
::    prompt -> Kurulum sÄ±rasÄ±nda kullanÄ±cÄ±ya sorar. (GÃ¼ncellemeyi reddetme seÃ§eneÄŸi bulunur.)
set spotx_update_mode=overwrite

:: Spotify ana sayfa iÃ§eriÄŸi:
:: remove -> Ana sayfadan podcastleri, bÃ¶lÃ¼mleri ve sesli kitaplarÄ± kaldÄ±rÄ±r.
::   keep -> Ana sayfada podcastleri, bÃ¶lÃ¼mleri ve sesli kitaplarÄ± tutar. (VarsayÄ±lan)
:: prompt -> Kurulum sÄ±rasÄ±nda kullanÄ±cÄ±ya sorar.
set spotx_homepage_content=remove

:: Spotify otomatik gÃ¼ncelleme seÃ§enekleri:
:: (Otomatik gÃ¼ncellemeler, SpotX'in yeniden yÃ¼klenmesini ve Spicetify'Ä± tekrar aktifleÅŸtirmek iÃ§in "spicetify apply" komutunun Ã§alÄ±ÅŸtÄ±rÄ±lmasÄ±nÄ± gerektirebilir.)
::  block -> Otomatik gÃ¼ncellemeleri engeller. (Ã–nerilen)
::  allow -> Otomatik gÃ¼ncellemelere izin verir. (VarsayÄ±lan)
:: prompt -> Kurulum sÄ±rasÄ±nda kullanÄ±cÄ±ya sorar.
set spotx_auto_updates=block

:: â•”â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•—
:: â•‘ Advanced Options â•‘
:: â•šâ•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•
:: Ä°ÅŸlemler Ã¶ncesi Spotify ayarlarÄ±nÄ± ve kullanÄ±cÄ± verilerini yedekler, iÅŸlemler tamamlandÄ±ÄŸÄ±nda geri yÃ¼kler. (true/false)
set backup=true

:: Hata durumunda bir sonraki iÅŸleme geÃ§meden Ã¶nce kullanÄ±cÄ± onayÄ± bekler. (true/false)
set pause=false

:: Ä°ÅŸlemler arasÄ±ndaki bekleme sÃ¼resi. (1-99 Saniye)
set delay=1

:: Ä°ÅŸlemler sÄ±rasÄ±nda ekstra hata ayÄ±klama bilgilerini gÃ¶sterir. (true/false)
set debug=false

:: â•”â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•—
:: â•‘  End of Config  â•‘
:: â•šâ•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•

if "%preset%" EQU "install" (
	set "spotify_uninstall=false" & set "spotify_install=true"
	set "spotx_uninstall=false" & set "spotx_install=true"
	set "spicetify_uninstall=false" & set "spicetify_install=true" & set "spicetify_update=false"
	set "backup=false"
) else if "%preset%" EQU "uninstall" (
	set "spotify_uninstall=true" & set "spotify_install=false"
	set "spotx_uninstall=true" & set "spotx_install=false"
	set "spicetify_uninstall=true" & set "spicetify_install=false" & set "spicetify_update=false"
	set "backup=false"
) else if "%preset%" EQU "reinstall" (
	set "spotify_uninstall=true" & set "spotify_install=true"
	set "spotx_uninstall=true" & set "spotx_install=true"
	set "spicetify_uninstall=true" & set "spicetify_install=true" & set "spicetify_update=false"
	set "backup=true"
) else if "%preset%" EQU "update" (
	set "spotify_uninstall=false" & set "spotify_install=false"
	set "spotx_uninstall=false" & set "spotx_install=false"
	set "spicetify_uninstall=false" & set "spicetify_install=false" & set "spicetify_update=true"
	set "backup=false"
)

if "%spicetify_install%" EQU "true" (
	if "%spicetify_update%" EQU "true" (
		if exist "%localappdata%\spicetify\spicetify.exe" (
			set "spicetify_install=false" & set "spicetify_update=true"
		) else (
			set "spicetify_install=true" & set "spicetify_update=false"
		)
	)
)

set "change=false"
if "%spotify_uninstall%" EQU "true" (set "change=true")
if "%spotify_install%" EQU "true" (set "change=true")
if "%spotx_uninstall%" EQU "true" (set "change=true")
if "%spotx_install%" EQU "true" (set "change=true")
if "%spicetify_uninstall%" EQU "true" (set "change=true")
if "%spicetify_install%" EQU "true" (set "change=true")
if "%spicetify_update%" EQU "true" (set "change=true")
if "%change%" EQU "false" (
	echo Herhangi bir iÅŸlem seÃ§ilmedi lÃ¼tfen dosyaya saÄŸ tÄ±klayÄ±p not defteri ile yapÄ±landÄ±rmayÄ± dÃ¼zenleyin.
	echo Ã‡Ä±kmak iÃ§in herhangi bir tuÅŸa basÄ±n... & endlocal & pause >nul 2>&1 & exit
)

set "config_error=false"
for %%v in (spotify_uninstall spotify_install spotx_uninstall spotx_install spicetify_uninstall spicetify_install spicetify_update backup pause debug) do (
    if "!%%v!" NEQ "true" if "!%%v!" NEQ "false" (
        echo [41;97m Error [0m '%%v' deÄŸiÅŸkeni iÃ§in geÃ§ersiz deÄŸer: '!%%v!', true/false olmalÄ±.
        set "config_error=true"
    )
)
if "%spotx_update_mode%" NEQ "overwrite" if "%spotx_update_mode%" NEQ "reinstall" if "%spotx_update_mode%" NEQ "prompt" (
    echo [41;97m Error [0m 'spotx_update_mode' deÄŸiÅŸkeni iÃ§in geÃ§ersiz deÄŸer: '%spotx_update_mode%'
    set "config_error=true"
)
if "%spotx_homepage_content%" NEQ "remove" if "%spotx_homepage_content%" NEQ "keep" if "%spotx_homepage_content%" NEQ "prompt" (
    echo [41;97m Error [0m 'spotx_homepage_content' deÄŸiÅŸkeni iÃ§in geÃ§ersiz deÄŸer: '%spotx_homepage_content%'
    set "config_error=true"
)
if "%spotx_auto_updates%" NEQ "block" if "%spotx_auto_updates%" NEQ "allow" if "%spotx_auto_updates%" NEQ "prompt" (
    echo [41;97m Error [0m 'spotx_auto_updates' deÄŸiÅŸkeni iÃ§in geÃ§ersiz deÄŸer: '%spotx_auto_updates%'
    set "config_error=true"
)
echo %delay%| findstr /r "^[0-9][0-9]*$" >nul 2>&1 || (
	echo [41;97m Error [0m 'delay' deÄŸiÅŸkeni iÃ§in geÃ§ersiz deÄŸer: '%delay%', 1-99 aralÄ±ÄŸÄ±nda olmalÄ±.
	set "config_error=true"
)
if "%config_error%" EQU "true" (
    echo LÃ¼tfen yapÄ±landÄ±rmayÄ± dÃ¼zeltip tekrar Ã§alÄ±ÅŸtÄ±rÄ±n.
    echo Ã‡Ä±kmak iÃ§in herhangi bir tuÅŸa basÄ±n... & endlocal & pause >nul 2>&1 & exit
)

if exist "%userprofile%\Desktop\Spotify.lnk" (
	set "spotify_desktop_shortcut=true"
) else (
	set "spotify_desktop_shortcut=false"
)

if "%backup%" EQU "true" (call :spotify_backup)
if "%spotify_uninstall%" EQU "true" (call :spotify_uninstall)
if "%spotx_uninstall%" EQU "true" (call :spotx_uninstall)
if "%spicetify_uninstall%" EQU "true" (call :spicetify_uninstall)
if "%spotify_install%" EQU "true" (call :spotify_install)
if "%spotx_install%" EQU "true" (call :spotx_install)
if "%spicetify_install%" EQU "true" (call :spicetify_install)
if "%spicetify_update%" EQU "true" (call :spicetify_update)
if "%backup%" EQU "true" (call :spotify_restore)

if "%spotify_desktop_shortcut%" NEQ "true" (
	del /q "%userprofile%\Desktop\Spotify.lnk" >nul 2>&1
)

for %%v in (
    spotify_uninstall_status
    spotify_install_status
    spotify_update_status
    spotx_uninstall_status
    spotx_install_status
    spotx_update_status
    spicetify_uninstall_status
    spicetify_install_status
    spicetify_update_status
	backup_status
	restore_status
) do (
    call :set_symbol %%v !%%v!
)

cls
echo â•”â•â•â•â•â•â•â•â•â•â•â•â•¦â•â•â•â•â•â•â•â•â•â•â•¦â•â•â•â•â•â•â•â•â•â•â•¦â•â•â•â•â•â•â•â•â•â•â•—
echo â•‘ [94mProg/Drum[0m â•‘  [94mKaldÄ±r[0m  â•‘  [94mYÃ¼kle[0m   â•‘ [94mGÃ¼ncelle[0m â•‘
echo â• â•â•â•â•â•â•â•â•â•â•â•â•¬â•â•â•â•â•â•â•â•â•â•â•¬â•â•â•â•â•â•â•â•â•â•â•¬â•â•â•â•â•â•â•â•â•â•â•£   â•”â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•—
echo â•‘ [92mSpotify[0m   â•‘    !spotify_uninstall_status!    â•‘    !spotify_install_status!    â•‘    !spotify_update_status!    â•‘   â•‘ âœ”ï¸ [90m-^>[0m Ä°ÅŸlem yapÄ±ldÄ± ve baÅŸarÄ±lÄ± oldu.  â•‘
echo â• â•â•â•â•â•â•â•â•â•â•â•â•¬â•â•â•â•â•â•â•â•â•â•â•¬â•â•â•â•â•â•â•â•â•â•â•¬â•â•â•â•â•â•â•â•â•â•â•£   â•‘ âŒ [90m-^>[0m Ä°ÅŸlem denendi ve baÅŸarÄ±sÄ±z oldu. â•‘
echo â•‘ [93mSpotX[0m     â•‘    !spotx_uninstall_status!    â•‘    !spotx_install_status!    â•‘    !spotx_update_status!    â•‘   â•‘ â– [90m-^>[0m Ä°ÅŸlem yapÄ±lmadÄ±/gerek yoktu.     â•‘
echo â• â•â•â•â•â•â•â•â•â•â•â•â•¬â•â•â•â•â•â•â•â•â•â•â•¬â•â•â•â•â•â•â•â•â•â•â•¬â•â•â•â•â•â•â•â•â•â•â•£   â•šâ•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•
echo â•‘ [91mSpicetify[0m â•‘    !spicetify_uninstall_status!    â•‘    !spicetify_install_status!    â•‘    !spicetify_update_status!    â•‘
echo â•šâ•â•â•â•â•â•â•â•â•â•â•â•©â•â•â•â•â•â•â•â•â•â•â•©â•â•â•â•â•â•â•â•â•â•â•©â•â•â•â•â•â•â•â•â•â•â•
echo [96mYedekleme:[0m !backup_status!  [96mGeri YÃ¼kleme:[0m !restore_status!
echo Ã‡Ä±kmak iÃ§in herhangi bir tuÅŸa basÄ±n... & endlocal & pause >nul 2>&1 & exit

:spotify_backup
cls & echo â•”â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•— & echo â•‘ Spotify Backup â•‘ & echo â•šâ•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•
if not exist "%appdata%\Spotify\prefs." (
	echo [33mYedeklenecek Spotify dosyasÄ± bulunamadÄ±.[0m
	timeout /t %delay% /nobreak >nul 2>&1
	exit /b
)
call :spo_stp
echo Spotify yedekleniyor...
rd /s /q "%temp%\SpotifyBackup" >nul 2>&1
xcopy "%appdata%\Spotify\prefs." "%temp%\SpotifyBackup\" /i /y >nul 2>&1
xcopy "%appdata%\Spotify\Users\" "%temp%\SpotifyBackup\Users\" /s /e /i /y >nul 2>&1
if exist "%temp%\SpotifyBackup\prefs." (
	set "backup_status=true"
	echo [32mSpotify baÅŸarÄ±yla yedeklendi.[0m
	timeout /t %delay% /nobreak >nul 2>&1
) else (
	set "backup_status=false"
	echo [31mSpotify yedeklenemedi.[0m
	if "%pause%" EQU "true" (echo Devam etmek iÃ§in herhangi bir tuÅŸa basÄ±n... & pause >nul 2>&1) else (timeout /t %delay% /nobreak >nul 2>&1)
)
exit /b

:spotify_uninstall
cls & echo â•”â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•— & echo â•‘ Spotify Uninstall â•‘ & echo â•šâ•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•
if not exist "%appdata%\Spotify\Spotify.exe" (
	echo [33mSpotify yÃ¼klÃ¼ deÄŸil, kaldÄ±rÄ±lamaz.[0m
	timeout /t %delay% /nobreak >nul 2>&1
	exit /b
)
call :spo_stp
echo Spotify kaldÄ±rÄ±lÄ±yor...
if exist "%appdata%\Spotify\Spotify.bak" (set "spotx_installed=true")
icacls "%localappdata%\Spotify\Update" /reset /t >nul 2>&1
start /b /w "SpotifyUninstall" "%appdata%\Spotify\Spotify.exe" /uninstall /silent
timeout /t 2 /nobreak >nul 2>&1
rd /s /q "%appdata%\Spotify" >nul 2>&1
rd /s /q "%localappdata%\Spotify" >nul 2>&1
del /q "%temp%\SpotifyUninstall.exe" >nul 2>&1
del /q "%userprofile%\Desktop\Spotify.lnk" >nul 2>&1
if not exist "%appdata%\Spotify\Spotify.exe" (
	if "%spotx_installed%" EQU "true" (set "spotx_uninstall_status=true")
	set "spotify_uninstall_status=true"
	echo [32mSpotify baÅŸarÄ±yla kaldÄ±rÄ±ldÄ±.[0m
	timeout /t %delay% /nobreak >nul 2>&1
) else (
	set "spotify_uninstall_status=false"
	echo [31mSpotify kaldÄ±rÄ±lamadÄ±.[0m
	if "%pause%" EQU "true" (echo Devam etmek iÃ§in herhangi bir tuÅŸa basÄ±n... & pause >nul 2>&1) else (timeout /t %delay% /nobreak >nul 2>&1)
)
exit /b

:spotx_uninstall
cls & echo â•”â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•— & echo â•‘ SpotX Uninstall â•‘ & echo â•šâ•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•
if not exist "%appdata%\Spotify\Spotify.bak" (
	echo [33mSpotX yÃ¼klÃ¼ deÄŸil, kaldÄ±rÄ±lamaz.[0m
	timeout /t %delay% /nobreak >nul 2>&1
	exit /b
)
call :spo_stp
echo SpotX kaldÄ±rÄ±lÄ±yor...
del /q "%appdata%\Spotify\dpapi.dll" >nul 2>&1
del /q "%appdata%\Spotify\Spotify.exe" >nul 2>&1
move "%appdata%\Spotify\Spotify.bak" "%appdata%\Spotify\Spotify.exe" >nul 2>&1
del /q "%appdata%\Spotify\config.ini" >nul 2>&1
del /q "%appdata%\Spotify\apps\xpui.spa" >nul 2>&1
move "%appdata%\Spotify\apps\xpui.bak" "%appdata%\Spotify\apps\xpui.spa" >nul 2>&1
del /q "%appdata%\Spotify\blockthespot_log.txt" >nul 2>&1
if not exist "%appdata%\Spotify\Spotify.bak" (
	set "spotx_uninstall_status=true"
	echo [32mSpotX baÅŸarÄ±yla kaldÄ±rÄ±ldÄ±.[0m
	timeout /t %delay% /nobreak >nul 2>&1
) else (
	set "spotx_uninstall_status=false"
	echo [31mSpotX kaldÄ±rÄ±lamadÄ±.[0m
	if "%pause%" EQU "true" (echo Devam etmek iÃ§in herhangi bir tuÅŸa basÄ±n... & pause >nul 2>&1) else (timeout /t %delay% /nobreak >nul 2>&1)
)
exit /b

:spicetify_uninstall
cls & echo â•”â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•— & echo â•‘ Spicetify Uninstall â•‘ & echo â•šâ•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•
if not exist "%localappdata%\spicetify\spicetify.exe" (
	echo [33mSpicetify yÃ¼klÃ¼ deÄŸil, kaldÄ±rÄ±lamaz.[0m
	timeout /t %delay% /nobreak >nul 2>&1
	exit /b
)
call :spo_stp
echo Spicetify kaldÄ±rÄ±lÄ±yor...
rd /s /q "%appdata%\spicetify" >nul 2>&1
rd /s /q "%localappdata%\spicetify" >nul 2>&1
if not exist "%localappdata%\spicetify\spicetify.exe" (
	set "spicetify_uninstall_status=true"
	echo [32mSpicetify baÅŸarÄ±yla kaldÄ±rÄ±ldÄ±.[0m
	timeout /t %delay% /nobreak >nul 2>&1
) else (
	set "spicetify_uninstall_status=false"
	echo [31mSpicetify kaldÄ±rÄ±lamadÄ±.[0m
	if "%pause%" EQU "true" (echo Devam etmek iÃ§in herhangi bir tuÅŸa basÄ±n... & pause >nul 2>&1) else (timeout /t %delay% /nobreak >nul 2>&1)
)
exit /b

:spotify_install
cls & echo â•”â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•— & echo â•‘ Spotify Install â•‘ & echo â•šâ•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•
if exist "%appdata%\Spotify\Spotify.exe" (
	echo [33mSpotify zaten yÃ¼klÃ¼.[0m
	timeout /t %delay% /nobreak >nul 2>&1
	exit /b
)
call :spo_stp
echo Spotify indiriliyor...
del /q "%temp%\SpotifySetup.exe" >nul 2>&1
powershell -ExecutionPolicy RemoteSigned -Command "Invoke-WebRequest -Uri 'https://download.scdn.co/SpotifySetup.exe' -OutFile '%temp%\SpotifySetup.exe' -UseBasicParsing"
if not exist "%temp%\SpotifySetup.exe" (
	set "spotify_install_status=false"
	echo [31mSpotify indirilemedi.[0m
	if "%pause%" EQU "true" (echo Devam etmek iÃ§in herhangi bir tuÅŸa basÄ±n... & pause >nul 2>&1) else (timeout /t %delay% /nobreak >nul 2>&1)
	exit /b
)
echo [32mSpotify baÅŸarÄ±yla indirildi.[0m
timeout /t %delay% /nobreak >nul 2>&1
echo Spotify yÃ¼kleniyor...
start /b /w "SpotifyInstall" "%temp%\SpotifySetup.exe" /silent >nul 2>&1
timeout /t 2 /nobreak >nul 2>&1
if exist "%appdata%\Spotify\Spotify.exe" (
	set "spotify_install_status=true"
	echo [32mSpotify baÅŸarÄ±yla yÃ¼klendi.[0m
	timeout /t %delay% /nobreak >nul 2>&1
) else (
	set "spotify_install_status=false"
	echo [31mSpotify yÃ¼klenemedi.[0m
	if "%pause%" EQU "true" (echo Devam etmek iÃ§in herhangi bir tuÅŸa basÄ±n... & pause >nul 2>&1) else (timeout /t %delay% /nobreak >nul 2>&1)
)
del /q "%temp%\SpotifySetup.exe" >nul 2>&1
exit /b

:spotx_install
cls & echo â•”â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•— & echo â•‘ SpotX Install â•‘ & echo â•šâ•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•
if "%spotx_update_mode%" EQU "overwrite" (
	set "spotx_update_mode= -confirm_spoti_recomended_over"
) else if "%spotx_update_mode%" EQU "reinstall" (
	set "spotx_update_mode= -confirm_spoti_recomended_uninstall"
) else (
	set "spotx_update_mode="
)
if "%spotx_homepage_content%" EQU "remove" (
	set "spotx_homepage_content= -podcasts_off"
) else if "%spotx_homepage_content%" EQU "keep" (
	set "spotx_homepage_content= -podcasts_on"
) else (
	set "spotx_homepage_content="
)
if "%spotx_auto_updates%" EQU "block" (
	set "spotx_auto_updates= -block_update_on"
) else if "%spotx_auto_updates%" EQU "allow" (
	set "spotx_auto_updates= -block_update_off"
) else (
	set "spotx_auto_updates="
)
set "param=!spotx_update_mode!!spotx_homepage_content!!spotx_auto_updates! -confirm_uninstall_ms_spoti -start_spoti"
if "%debug%" EQU "true" (echo [45;97m Debug [0m param:!param! & echo Devam etmek iÃ§in herhangi bir tuÅŸa basÄ±n... & pause >nul 2>&1)
if not exist "%appdata%\Spotify\Spotify.exe" (
	set "spotx_install_status=false"
	echo [31mSpotify yÃ¼klÃ¼ deÄŸil, SpotX yÃ¼klenemez.[0m
	if "%pause%" EQU "true" (echo Devam etmek iÃ§in herhangi bir tuÅŸa basÄ±n... & pause >nul 2>&1) else (timeout /t %delay% /nobreak >nul 2>&1)
	exit /b
)
if exist "%appdata%\Spotify\Spotify.bak" (
	echo [33mSpotX zaten yÃ¼klÃ¼.[0m
	timeout /t %delay% /nobreak >nul 2>&1
	exit /b
)
call :spo_stp
echo SpotX yÃ¼kleniyor... 
powershell -ExecutionPolicy RemoteSigned -Command "Invoke-Expression ""& { $(Invoke-WebRequest -Uri 'https://raw.githubusercontent.com/SpotX-Official/spotx-official.github.io/main/run.ps1' -UseBasicParsing) } !param!"""
if exist "%appdata%\Spotify\Spotify.bak" (
	set "spotx_install_status=true"
	echo [32mSpotX baÅŸarÄ±yla yÃ¼klendi.[0m
	timeout /t %delay% /nobreak >nul 2>&1
) else (
	set "spotx_install_status=false"
	echo [31mSpotX yÃ¼klenemedi.[0m
	if "%pause%" EQU "true" (echo Devam etmek iÃ§in herhangi bir tuÅŸa basÄ±n... & pause >nul 2>&1) else (timeout /t %delay% /nobreak >nul 2>&1)
)
exit /b

:spicetify_install
cls & echo â•”â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•— & echo â•‘ Spicetify Install â•‘ & echo â•šâ•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•
if not exist "%appdata%\Spotify\Spotify.exe" (
	set "spicetify_install_status=false"
	echo [31mSpotify yÃ¼klÃ¼ deÄŸil, Spicetify yÃ¼klenemez.[0m
	if "%pause%" EQU "true" (echo Devam etmek iÃ§in herhangi bir tuÅŸa basÄ±n... & pause >nul 2>&1) else (timeout /t %delay% /nobreak >nul 2>&1)
	exit /b
)
if exist "%localappdata%\spicetify\spicetify.exe" (
	echo [33mSpicetify zaten yÃ¼klÃ¼.[0m
	timeout /t %delay% /nobreak >nul 2>&1
	exit /b
)
call :spo_stp
echo Spicetify yÃ¼kleniyor... 
powershell -ExecutionPolicy RemoteSigned -Command "Invoke-WebRequest -Uri 'https://raw.githubusercontent.com/spicetify/cli/main/install.ps1' -UseBasicParsing | Invoke-Expression"
if exist "%localappdata%\spicetify\spicetify.exe" (
	set "spicetify_install_status=true"
	echo [32mSpicetify baÅŸarÄ±yla yÃ¼klendi.[0m
	timeout /t %delay% /nobreak >nul 2>&1
) else (
	set "spicetify_install_status=false"
	echo [31mSpicetify yÃ¼klenemedi.[0m
	if "%pause%" EQU "true" (echo Devam etmek iÃ§in herhangi bir tuÅŸa basÄ±n... & pause >nul 2>&1) else (timeout /t %delay% /nobreak >nul 2>&1)
)
exit /b

:spicetify_update
cls & echo â•”â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•— & echo â•‘ Spicetify Update â•‘ & echo â•šâ•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•
if not exist "%appdata%\Spotify\Spotify.exe" (
	set "spicetify_update_status=false"
	echo [31mSpotify yÃ¼klÃ¼ deÄŸil, Spicetify gÃ¼ncellenemez.[0m
	if "%pause%" EQU "true" (echo Devam etmek iÃ§in herhangi bir tuÅŸa basÄ±n... & pause >nul 2>&1) else (timeout /t %delay% /nobreak >nul 2>&1)
	exit /b
)
if not exist "%localappdata%\spicetify\spicetify.exe" (
	set "spicetify_update_status=false"
	echo [31mSpicetify yÃ¼klÃ¼ deÄŸil, GÃ¼ncellenemez.[0m
	if "%pause%" EQU "true" (echo Devam etmek iÃ§in herhangi bir tuÅŸa basÄ±n... & pause >nul 2>&1) else (timeout /t %delay% /nobreak >nul 2>&1)
	exit /b
)
call :spo_stp
echo Spicetify gÃ¼ncelleniyor...
for /f %%a in ('spicetify --version') do (set "old_spi_ver=%%a")
if "%debug%" EQU "true" (echo [45;97m Debug [0m old_spi_ver:!old_spi_ver!)
powershell -ExecutionPolicy RemoteSigned -Command "spicetify update"
timeout /t 2 /nobreak >nul 2>&1
for /f %%a in ('spicetify --version') do (set "new_spi_ver=%%a")
if "%debug%" EQU "true" (echo [45;97m Debug [0m new_spi_ver:!new_spi_ver!)
if "!old_spi_ver!" NEQ "!new_spi_ver!" (
	set "spicetify_update_status=true"
	echo [32mSpicetify baÅŸarÄ±yla gÃ¼ncellendi:[0m '!old_spi_ver!' [90m-^>[0m '!new_spi_ver!'
	timeout /t %delay% /nobreak >nul 2>&1
) else (
	set "spicetify_update_status=false"
	echo [33mSpicetify gÃ¼ncellenemedi veya zaten gÃ¼ncel:[0m '!old_spi_ver!' [90m-^>[0m '!new_spi_ver!'
	if "%pause%" EQU "true" (echo Devam etmek iÃ§in herhangi bir tuÅŸa basÄ±n... & pause >nul 2>&1) else (timeout /t %delay% /nobreak >nul 2>&1)
)
exit /b

:spotify_restore
cls & echo â•”â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•— & echo â•‘ Spotify Restore â•‘ & echo â•šâ•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•
if not exist "%appdata%\Spotify\Spotify.exe" (
	set "restore_status=false"
	echo [31mSpotify yÃ¼klÃ¼ deÄŸil, yedek geri yÃ¼klenemez.[0m
	if "%pause%" EQU "true" (echo Devam etmek iÃ§in herhangi bir tuÅŸa basÄ±n... & pause >nul 2>&1) else (timeout /t %delay% /nobreak >nul 2>&1)
	exit /b
)
if not exist "%temp%\SpotifyBackup\prefs." (
	echo [33mSpotify yedeÄŸi bulunamadÄ±, geri yÃ¼klenemez[0m
	timeout /t %delay% /nobreak >nul 2>&1
	exit /b
)
call :spo_stp
echo Spotify yedeÄŸi geri yÃ¼kleniyor...
move /y "%appdata%\Spotify\prefs." "%appdata%\Spotify\prefs.backup" >nul 2>&1
xcopy "%temp%\SpotifyBackup\prefs." "%appdata%\Spotify\" /i /y >nul 2>&1
xcopy "%temp%\SpotifyBackup\Users\" "%appdata%\Spotify\Users\" /s /e /i /y >nul 2>&1
if exist "%appdata%\Spotify\prefs." (
	del /q "%appdata%\Spotify\prefs.backup" >nul 2>&1
	rd /s /q "%temp%\SpotifyBackup" >nul 2>&1
	set "restore_status=true"
	echo [32mSpotify yedeÄŸi baÅŸarÄ±yla geri yÃ¼klendi.[0m
	timeout /t %delay% /nobreak >nul 2>&1
) else (
	move /y "%appdata%\Spotify\prefs.backup" "%appdata%\Spotify\prefs." >nul 2>&1
	set "restore_status=false"
	echo [31mSpotify yedeÄŸi geri yÃ¼klenemedi.[0m
	if "%pause%" EQU "true" (echo Devam etmek iÃ§in herhangi bir tuÅŸa basÄ±n... & pause >nul 2>&1) else (timeout /t %delay% /nobreak >nul 2>&1)
)
exit /b

:spo_stp
if not exist "%appdata%\Spotify\Spotify.exe" (
	echo [33mSpotify bulunamadÄ±, durdurulamaz.[0m
	timeout /t %delay% /nobreak >nul 2>&1
	exit /b
)
set "retry=1"
set "max_retry=3"
:retry
tasklist | findstr "Spotify.exe" >nul 2>&1
if "%debug%" EQU "true" (echo [45;97m Debug [0m tasklist_1:!errorlevel!)
if "!errorlevel!" EQU "1" (
	echo [33mSpotify zaten durdurulmuÅŸ.[0m
	timeout /t %delay% /nobreak >nul 2>&1
	exit /b
)
echo Spotify durduruluyor...
taskkill /f /im "Spotify.exe" >nul 2>&1
timeout /t 2 /nobreak >nul 2>&1
tasklist | findstr "Spotify.exe" >nul 2>&1
if "%debug%" EQU "true" (echo [45;97m Debug [0m tasklist_2:!errorlevel!)
if "!errorlevel!" EQU "1" (
	echo [32mSpotify baÅŸarÄ±yla durduruldu.[0m
	timeout /t %delay% /nobreak >nul 2>&1
) else (
	if "%debug%" EQU "true" (echo [45;97m Debug [0m retry:%retry%)
	if "%retry%" GEQ "%max_retry%" (
		echo Spotify %retry% kez denemenin ardÄ±ndan durdurulamadÄ±, Spotify'Ä± manuel olarak kapatmayÄ± deneyin.
		echo Ã‡Ä±kmak iÃ§in herhangi bir tuÅŸa basÄ±n... & endlocal & pause >nul 2>&1 & exit
	)
	echo [31mSpotify durdurulamadÄ±, tekrar deneniyor...[0m
	timeout /t %delay% /nobreak >nul 2>&1
	set /a "retry+=1"
	goto :retry
)
exit /b

:set_symbol
set "varname=%~1"
set "value=%~2"
if "%value%" EQU "true" (
    set "%varname%=âœ”ï¸"
) else if "%value%" EQU "false" (
    set "%varname%=âŒ"
) else (
    set "%varname%=â–"
)
exit /b
