@echo off
title SpotUp
setlocal enabledelayedexpansion
chcp 65001 >nul 2>&1

:: â•”â•â•â•â•â•â•â•â•â•â•â•â•â•—
:: â•‘  Websites  â•‘
:: â•šâ•â•â•â•â•â•â•â•â•â•â•â•â•
::   Spotify: https://www.spotify.com/download | https://cutt.ly/8EH6NuH
:: Spicetify: https://github.com/spicetify/spicetify-cli | https://spicetify.app
::     SpotX: https://github.com/SpotX-Official/SpotX | https://github.com/SpotX-Official/SpotX/discussions/60
::    SpotUp: https://github.com/b12robot/SpotUp

:: â•”â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•—
:: â•‘ SpotUp Config â•‘
:: â•šâ•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•
::   install -> Spotify, SpotX ve Spicetify kurulumu yapar.
:: uninstall -> Spotify, SpotX ve Spicetify'i kaldÄ±rÄ±r.
:: reinstall -> Spotify, SpotX ve Spicetify'i kaldÄ±rÄ±p yeniden kurar.
::    update -> Spicetify'i gÃ¼nceller.
::    custom -> AÅŸaÄŸÄ±daki seÃ§eneklere gÃ¶re iÅŸlem yapar.
set preset=custom

:: Spotify: Dijital mÃ¼zik ve podcast akÄ±ÅŸ platformu.
:: SeÃ§enekler: true/false
:: spotify_uninstall -> Spotify'Ä± kaldÄ±r.
::   spotify_install -> Spotify'Ä± kur.
set spotify_uninstall=true
set spotify_install=true

:: SpotX: Spotify iÃ§in reklam engelleme ve gÃ¼ncelleme kontrol aracÄ±.
:: SeÃ§enekler: true/false
:: spotx_uninstall -> SpotX'i kaldÄ±r.
::   spotx_install -> SpotX'i kur.
set spotx_uninstall=true
set spotx_install=true

:: Spicetify: Spotifyâ€™Ä±n arayÃ¼zÃ¼nÃ¼ ve iÅŸlevlerini Ã¶zelleÅŸtirme aracÄ±.
:: SeÃ§enekler: true/false
:: spicetify_uninstall -> Spicetify'Ä± kaldÄ±r.
::   spicetify_install -> Spicetify'Ä± kur.
::    spicetify_update -> Spicetify'Ä± gÃ¼ncelle.
set spicetify_uninstall=false
set spicetify_install=false
set spicetify_update=false

:: â•”â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•—
:: â•‘ SpotX Config â•‘
:: â•šâ•â•â•â•â•â•â•â•â•â•â•â•â•â•â•
:: SpotX'in Spotify gÃ¼ncelleme yÃ¶ntemleri:
:: (YalnÄ±zca SpotX kurulumu sÄ±rasÄ±nda Spotify gÃ¼ncellemesi tespit edilirse geÃ§erlidir.)
:: overwrite -> Mevcut sÃ¼rÃ¼mÃ¼n Ã¼zerine yazarak gÃ¼nceller.
:: reinstall -> Mevcut sÃ¼rÃ¼mÃ¼ kaldÄ±rÄ±p baÅŸtan yÃ¼kler.
::    prompt -> Kurulum sÄ±rasÄ±nda kullanÄ±cÄ±ya sorar. (GÃ¼ncellemeyi reddetme seÃ§eneÄŸi bulunur.)
set spotx_update_mode=overwrite

:: Spotify ana sayfa iÃ§eriÄŸi:
:: remove -> Ana sayfadan podcastleri, bÃ¶lÃ¼mleri ve sesli kitaplarÄ± kaldÄ±rÄ±r.
::   keep -> Ana sayfada podcastleri, bÃ¶lÃ¼mleri ve sesli kitaplarÄ± tutar. (VarsayÄ±lan)
:: prompt -> Kurulum sÄ±rasÄ±nda kullanÄ±cÄ±ya sorar.
set spotx_homepage_content=remove

:: Spotify otomatik gÃ¼ncelleme seÃ§enekleri:
:: (Otomatik gÃ¼ncellemeler, SpotX'in yeniden yÃ¼klenmesini ve Spicetify'Ä± tekrar aktifleÅŸtirmek iÃ§in "spicetify apply" komutunun Ã§alÄ±ÅŸtÄ±rÄ±lmasÄ±nÄ± gerektirebilir.)
::  block -> Otomatik gÃ¼ncellemeleri engeller. (Ã–nerilen)
::  allow -> Otomatik gÃ¼ncellemelere izin verir. (VarsayÄ±lan)
:: prompt -> Kurulum sÄ±rasÄ±nda kullanÄ±cÄ±ya sorar.
set spotx_auto_updates=block

set "backup=true"
set "pause=false"
set "debug=false"
set "delay=1"

if "%debug%" EQU "true" (echo [45;97m Debug [0m preset:%preset%)
if "%preset%" EQU "install" (
	set "spotify_uninstall=false" & set "spotify_install=true" & set "spotify_update=false"
	set "spotx_uninstall=false" & set "spotx_install=true" & set "spotx_update=false"
	set "spicetify_uninstall=false" & set "spicetify_install=true" & set "spicetify_update=false"
) else if "%preset%" EQU "uninstall" (
	set "spotify_uninstall=true" & set "spotify_install=false" & set "spotify_update=false"
	set "spotx_uninstall=true" & set "spotx_install=false" & set "spotx_update=false"
	set "spicetify_uninstall=true" & set "spicetify_install=false" & set "spicetify_update=false"
) else if "%preset%" EQU "reinstall" (
	set "spotify_uninstall=true" & set "spotify_install=true" & set "spotify_update=false"
	set "spotx_uninstall=true" & set "spotx_install=true" & set "spotx_update=false"
	set "spicetify_uninstall=true" & set "spicetify_install=true" & set "spicetify_update=false"
) else if "%preset%" EQU "update" (
	set "spotify_uninstall=false" & set "spotify_install=false" & set "spotify_update=true"
	set "spotx_uninstall=false" & set "spotx_install=false" & set "spotx_update=true"
	set "spicetify_uninstall=false" & set "spicetify_install=false" & set "spicetify_update=true"
)

if "%debug%" EQU "true" (echo [45;97m Debug [0m spotify_uninstall:%spotify_uninstall%)
if "%debug%" EQU "true" (echo [45;97m Debug [0m spotify_install:%spotify_install%)
if "%debug%" EQU "true" (echo [45;97m Debug [0m spotx_uninstall:%spotx_uninstall%)
if "%debug%" EQU "true" (echo [45;97m Debug [0m spotx_install:%spotx_install%)
if "%debug%" EQU "true" (echo [45;97m Debug [0m spicetify_uninstall:%spicetify_uninstall%)
if "%debug%" EQU "true" (echo [45;97m Debug [0m spicetify_install:%spicetify_install%)
if "%debug%" EQU "true" (echo [45;97m Debug [0m spicetify_update:%spicetify_update%)

set "change=0"
if "%spotify_uninstall%" NEQ "true" (set /a "change+=1")
if "%spotify_install%" NEQ "true" (set /a "change+=1")
if "%spotify_update%" NEQ "true" (set /a "change+=1")
if "%spotx_uninstall%" NEQ "true" (set /a "change+=1")
if "%spotx_install%" NEQ "true" (set /a "change+=1")
if "%spotx_update%" NEQ "true" (set /a "change+=1")
if "%spicetify_uninstall%" NEQ "true" (set /a "change+=1")
if "%spicetify_install%" NEQ "true" (set /a "change+=1")
if "%spicetify_update%" NEQ "true" (set /a "change+=1")
if "%debug%" EQU "true" (echo [45;97m Debug [0m change:%change%)
if "%change%" EQU "9" (
	echo Herhangi bir iÅŸlem seÃ§ilmedi lÃ¼tfen dosyaya saÄŸ tÄ±klayÄ±p not defteri ile yapÄ±landÄ±rmayÄ± dÃ¼zenleyin.
	echo Ã‡Ä±kmak iÃ§in herhangi bir tuÅŸa basÄ±n... & endlocal & pause >nul 2>&1 & exit
)

if "%spicetify_install%" EQU "true" (
	if "%spicetify_update%" EQU "true" (
		if exist "%localappdata%\spicetify\spicetify.exe" (
			set "spicetify_install=false" & set "spicetify_update=true"
		) else (
			set "spicetify_install=true" & set "spicetify_update=false"
		)
	)
)

if "%spotify_uninstall%" EQU "true" (
	cls & echo â•”â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•— & echo â•‘ Spotify Uninstall â•‘ & echo â•šâ•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•
	if not exist "%appdata%\Spotify\Spotify.exe" (
		echo [33mSpotify yÃ¼klÃ¼ deÄŸil, kaldÄ±rÄ±lamaz.[0m
		timeout /t %delay% /nobreak >nul 2>&1
	) else (
		call :spo_stp
		if exist "%userprofile%\Desktop\Spotify.lnk" (
			set "spotify_desktop_shortcut=true"
		) else (
			set "spotify_desktop_shortcut=false"
		)
		if "%backup%" EQU "true" (
			if exist "%appdata%\Spotify\prefs." (
				echo Spotify yedekleniyor...
				xcopy "%appdata%\Spotify\prefs." "%temp%\SpotifyBackup\" /i /y >nul 2>&1
				xcopy "%appdata%\Spotify\Users\prefs." "%temp%\SpotifyBackup\Users\" /s /e /i /y >nul 2>&1
				xcopy "%localappdata%\Spotify\Local State." "%temp%\SpotifyBackup\" /i /y >nul 2>&1
				if exist "%temp%\SpotifyBackup\prefs." (
					echo [32mSpotify baÅŸarÄ±yla yedeklendi.[0m
					timeout /t %delay% /nobreak >nul 2>&1
				) else (
					echo [31mSpotify yedeklenemedi.[0m
					if "%pause%" EQU "true" (echo Devam etmek iÃ§in herhangi bir tuÅŸa basÄ±n... & pause >nul 2>&1) else (timeout /t %delay% /nobreak >nul 2>&1)
				)
			) else (
				echo [33mYedeklenecek Spotify dosyasÄ± bulunamadÄ±.[0m
				if "%pause%" EQU "true" (echo Devam etmek iÃ§in herhangi bir tuÅŸa basÄ±n... & pause >nul 2>&1) else (timeout /t %delay% /nobreak >nul 2>&1)
			)
		)
		echo Spotify kaldÄ±rÄ±lÄ±yor...
		if exist "%localappdata%\Spotify\Update" (
			icacls "%localappdata%\Spotify\Update" /reset /t >nul 2>&1
		)
		if exist "%appdata%\Spotify\Spotify.exe" (
			start /b /w "SpotifyUninstall" "%appdata%\Spotify\Spotify.exe" /uninstall /silent
			timeout /t 2 /nobreak >nul 2>&1
		)
		if exist "%appdata%\Spotify" (
			rd /s /q "%appdata%\Spotify" >nul 2>&1
		)
		if exist "%localappdata%\Spotify" (
			rd /s /q "%localappdata%\Spotify" >nul 2>&1
		)
		if exist "%temp%\SpotifyUninstall.exe" (
			del /q  "%temp%\SpotifyUninstall.exe" >nul 2>&1
		)
		if exist "%userprofile%\Desktop\Spotify.lnk" (
			del /q "%userprofile%\Desktop\Spotify.lnk" >nul 2>&1
		)
		if not exist "%appdata%\Spotify\Spotify.exe" (
			set "spotify_uninstall_status=true"
			echo [32mSpotify baÅŸarÄ±yla kaldÄ±rÄ±ldÄ±.[0m
			timeout /t %delay% /nobreak >nul 2>&1
		) else (
			set "spotify_uninstall_status=false"
			echo [31mSpotify kaldÄ±rÄ±lamadÄ±.[0m
			if "%pause%" EQU "true" (echo Devam etmek iÃ§in herhangi bir tuÅŸa basÄ±n... & pause >nul 2>&1) else (timeout /t %delay% /nobreak >nul 2>&1)
		)
	)
)

if "%spotx_uninstall%" EQU "true" (
	cls & echo â•”â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•— & echo â•‘ SpotX Uninstall â•‘ & echo â•šâ•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•
	if not exist "%appdata%\Spotify" (
		set "spotx_uninstall_status=true"
		echo [33mSpotX zaten kaldÄ±rÄ±lmÄ±ÅŸ.[0m
		timeout /t %delay% /nobreak >nul 2>&1
	) else (
		if not exist "%appdata%\Spotify\Spotify.bak" (
			echo [33mSpotX yÃ¼klÃ¼ deÄŸil, kaldÄ±rÄ±lamaz.[0m
			timeout /t %delay% /nobreak >nul 2>&1
		) else (
			call :spo_stp
			echo SpotX kaldÄ±rÄ±lÄ±yor...
			if exist "%appdata%\Spotify\dpapi.dll" (
				del /q "%appdata%\Spotify\dpapi.dll" >nul 2>&1
			)
			if exist "%appdata%\Spotify\Spotify.bak" (
				del /q "%appdata%\Spotify.exe" >nul 2>&1
				move "%appdata%\Spotify\Spotify.bak" "%appdata%\Spotify\Spotify.exe" >nul 2>&1
			)
			if exist "%appdata%\Spotify\config.ini" (
				del /q "%appdata%\Spotify\config.ini" >nul 2>&1
			)
			if exist "%appdata%\Spotify\apps\xpui.bak" (
				del /q "%appdata%\Spotify\apps\xpui.spa" >nul 2>&1
				move "%appdata%\Spotify\apps\xpui.bak" "%appdata%\Spotify\apps\xpui.spa" >nul 2>&1
			)
			if exist "%appdata%\Spotify\blockthespot_log.txt" (
				del /q "%appdata%\Spotify\blockthespot_log.txt" >nul 2>&1
			)
			if exist "%temp%\spotx_temp*" (
				for /d %%i in ("%temp%\spotx_temp*") do (
					rd /s /q "%%i" >nul 2>&1
				)
			)
			if not exist "%appdata%\Spotify\Spotify.bak" (
				set "spotx_uninstall_status=true"
				echo [32mSpotX baÅŸarÄ±yla kaldÄ±rÄ±ldÄ±.[0m
				timeout /t %delay% /nobreak >nul 2>&1
			) else (
				set "spotx_uninstall_status=false"
				echo [31mSpotX kaldÄ±rÄ±lamadÄ±.[0m
				if "%pause%" EQU "true" (echo Devam etmek iÃ§in herhangi bir tuÅŸa basÄ±n... & pause >nul 2>&1) else (timeout /t %delay% /nobreak >nul 2>&1)
			)
		)
	)
)

if "%spicetify_uninstall%" EQU "true" (
	cls & echo â•”â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•— & echo â•‘ Spicetify Uninstall â•‘ & echo â•šâ•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•
	if not exist "%localappdata%\spicetify\spicetify.exe" (
		echo [33mSpicetify yÃ¼klÃ¼ deÄŸil, kaldÄ±rÄ±lamaz.[0m
		timeout /t %delay% /nobreak >nul 2>&1
	) else (
		call :spo_stp
		echo Spicetify kaldÄ±rÄ±lÄ±yor...
		if exist "%appdata%\spicetify" (
			rd /s /q "%appdata%\spicetify" >nul 2>&1
		)
		if exist "%localappdata%\spicetify" (
			rd /s /q "%localappdata%\spicetify" >nul 2>&1
		)
		if not exist "%localappdata%\spicetify\spicetify.exe" (
			set "spicetify_uninstall_status=true"
			echo [32mSpicetify baÅŸarÄ±yla kaldÄ±rÄ±ldÄ±.[0m
			timeout /t %delay% /nobreak >nul 2>&1
		) else (
			set "spicetify_uninstall_status=false"
			echo [31mSpicetify kaldÄ±rÄ±lamadÄ±.[0m
			if "%pause%" EQU "true" (echo Devam etmek iÃ§in herhangi bir tuÅŸa basÄ±n... & pause >nul 2>&1) else (timeout /t %delay% /nobreak >nul 2>&1)
		)
	)
)

if "%spotify_install%" EQU "true" (
	cls & echo â•”â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•— & echo â•‘ Spotify Install â•‘ & echo â•šâ•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•
	if exist "%appdata%\Spotify\Spotify.exe" (
		echo [33mSpotify zaten yÃ¼klÃ¼.[0m
		timeout /t %delay% /nobreak >nul 2>&1
	) else (
		call :spo_stp
		echo Spotify indiriliyor...
		if exist "%temp%\SpotifySetup.exe" (
			del /q "%temp%\SpotifySetup.exe" >nul 2>&1
		)
		powershell -ExecutionPolicy RemoteSigned -Command "Invoke-WebRequest -Uri 'https://download.scdn.co/SpotifySetup.exe' -OutFile '%temp%\SpotifySetup.exe' -UseBasicParsing"
		if exist "%temp%\SpotifySetup.exe" (
			echo [32mSpotify baÅŸarÄ±yla indirildi.[0m
			timeout /t %delay% /nobreak >nul 2>&1
			echo Spotify yÃ¼kleniyor...
			start /b /w "SpotifyInstall" "%temp%\SpotifySetup.exe" /silent >nul 2>&1
			timeout /t 2 /nobreak >nul 2>&1
			if exist "%appdata%\Spotify\Spotify.exe" (
				set "spotify_install_status=true"
				echo [32mSpotify baÅŸarÄ±yla yÃ¼klendi.[0m
				timeout /t %delay% /nobreak >nul 2>&1
			) else (
				set "spotify_install_status=false"
				echo [31mSpotify yÃ¼klenemedi.[0m
				if "%pause%" EQU "true" (echo Devam etmek iÃ§in herhangi bir tuÅŸa basÄ±n... & pause >nul 2>&1) else (timeout /t %delay% /nobreak >nul 2>&1)
			)
			if exist "%temp%\SpotifySetup.exe" (
				del /q "%temp%\SpotifySetup.exe" >nul 2>&1
			)
		) else (
			set "spotify_install_status=false"
			echo [31mSpotify indirilemedi.[0m
			if "%pause%" EQU "true" (echo Devam etmek iÃ§in herhangi bir tuÅŸa basÄ±n... & pause >nul 2>&1) else (timeout /t %delay% /nobreak >nul 2>&1)
		)
	)
)

if "%spotx_install%" EQU "true" (
	cls & echo â•”â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•— & echo â•‘ SpotX Install â•‘ & echo â•šâ•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•
	if "%debug%" EQU "true" (echo [45;97m Debug [0m spotx_update_mode:%spotx_update_mode%)
	if "%spotx_update_mode%" EQU "overwrite" (
		set "spotx_update_mode= -confirm_spoti_recomended_over"
	) else if "%spotx_update_mode%" EQU "reinstall" (
		set "spotx_update_mode= -confirm_spoti_recomended_uninstall"
	) else (
		set "spotx_update_mode="
	)
	if "%debug%" EQU "true" (echo [45;97m Debug [0m spotx_homepage_content:%spotx_homepage_content%)
	if "%spotx_homepage_content%" EQU "remove" (
		set "spotx_homepage_content= -podcasts_off"
	) else if "%spotx_homepage_content%" EQU "keep" (
		set "spotx_homepage_content= -podcasts_on"
	) else (
		set "spotx_homepage_content="
	)
	if "%debug%" EQU "true" (echo [45;97m Debug [0m spotx_auto_updates:%spotx_auto_updates%)
	if "%spotx_auto_updates%" EQU "block" (
		set "spotx_auto_updates= -block_update_on"
	) else if "%spotx_auto_updates%" EQU "allow" (
		set "spotx_auto_updates= -block_update_off"
	) else (
		set "spotx_auto_updates="
	)
	set "param=!spotx_update_mode!!spotx_homepage_content!!spotx_auto_updates! -confirm_uninstall_ms_spoti -start_spoti"
	if "%debug%" EQU "true" (echo [45;97m Debug [0m param:!param! & pause)
	if not exist "%appdata%\Spotify\Spotify.exe" (
		echo [31mSpotify yÃ¼klÃ¼ deÄŸil, SpotX yÃ¼klenemez.[0m
		timeout /t %delay% /nobreak >nul 2>&1
	) else (
		if exist "%appdata%\Spotify\Spotify.bak" (
			echo [33mSpotX zaten yÃ¼klÃ¼.[0m
			timeout /t %delay% /nobreak >nul 2>&1
		) else (
			call :spo_stp
			echo SpotX yÃ¼kleniyor... 
			powershell -ExecutionPolicy RemoteSigned -Command "Invoke-Expression ""& { $(Invoke-WebRequest -Uri 'https://raw.githubusercontent.com/SpotX-Official/spotx-official.github.io/main/run.ps1' -UseBasicParsing) } !param!"""
			if exist "%appdata%\Spotify\Spotify.bak" (
				set "spotx_install_status=true"
				echo [32mSpotX baÅŸarÄ±yla yÃ¼klendi.[0m
				timeout /t %delay% /nobreak >nul 2>&1
			) else (
				set "spotx_install_status=false"
				echo [31mSpotX yÃ¼klenemedi.[0m
				if "%pause%" EQU "true" (echo Devam etmek iÃ§in herhangi bir tuÅŸa basÄ±n... & pause >nul 2>&1) else (timeout /t %delay% /nobreak >nul 2>&1)
			)
		)
	)
)

if "%spicetify_install%" EQU "true" (
	cls & echo â•”â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•— & echo â•‘ Spicetify Install â•‘ & echo â•šâ•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•
	if not exist "%appdata%\Spotify\Spotify.exe" (
		echo [31mSpotify yÃ¼klÃ¼ deÄŸil, Spicetify yÃ¼klenemez.[0m
		timeout /t %delay% /nobreak >nul 2>&1
	) else (
		if exist "%localappdata%\spicetify\spicetify.exe" (
			echo [33mSpicetify zaten yÃ¼klÃ¼.[0m
			timeout /t %delay% /nobreak >nul 2>&1
		) else (
			call :spo_stp
			echo Spicetify yÃ¼kleniyor... 
			powershell -ExecutionPolicy RemoteSigned -Command "Invoke-WebRequest -Uri 'https://raw.githubusercontent.com/spicetify/cli/main/install.ps1' -UseBasicParsing | Invoke-Expression"
			if exist "%localappdata%\spicetify\spicetify.exe" (
				set "spicetify_install_status=true"
				echo [32mSpicetify baÅŸarÄ±yla yÃ¼klendi.[0m
				timeout /t %delay% /nobreak >nul 2>&1
			) else (
				set "spicetify_install_status=false"
				echo [31mSpicetify yÃ¼klenemedi.[0m
				if "%pause%" EQU "true" (echo Devam etmek iÃ§in herhangi bir tuÅŸa basÄ±n... & pause >nul 2>&1) else (timeout /t %delay% /nobreak >nul 2>&1)
			)
		)
	)
)

if "%spicetify_update%" EQU "true" (
	cls & echo â•”â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•— & echo â•‘ Spicetify Update â•‘ & echo â•šâ•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•
	if not exist "%appdata%\Spotify\Spotify.exe" (
		echo [31mSpotify yÃ¼klÃ¼ deÄŸil, Spicetify gÃ¼ncellenemez.[0m
		timeout /t %delay% /nobreak >nul 2>&1
	) else (
		if not exist "%localappdata%\spicetify\spicetify.exe" (
			echo [31mSpicetify yÃ¼klÃ¼ deÄŸil, GÃ¼ncellenemez.[0m
			timeout /t %delay% /nobreak >nul 2>&1
		) else (
			call :spo_stp
			echo Spicetify gÃ¼ncelleniyor...
			for /f %%a in ('spicetify --version') do set "old_spi_ver=%%a"
			if "%debug%" EQU "true" (echo [45;97m Debug [0m old_spi_ver:!old_spi_ver!)
			powershell -ExecutionPolicy RemoteSigned -Command "spicetify update"
			timeout /t 2 /nobreak >nul 2>&1
			for /f %%a in ('spicetify --version') do set "new_spi_ver=%%a"
			if "%debug%" EQU "true" (echo [45;97m Debug [0m new_spi_ver:!new_spi_ver!)
			if "!old_spi_ver!" EQU "!new_spi_ver!" (
				set "spicetify_update_status=false"
				echo [31mSpicetify gÃ¼ncellenemedi.[0m
				if "%pause%" EQU "true" (echo Devam etmek iÃ§in herhangi bir tuÅŸa basÄ±n... & pause >nul 2>&1) else (timeout /t %delay% /nobreak >nul 2>&1)
			) else (
				set "spicetify_update_status=true"
				echo [32mSpicetify baÅŸarÄ±yla gÃ¼ncellendi.[0m
				timeout /t %delay% /nobreak >nul 2>&1
			)
		)
	)
)

if "%spotify_uninstall%" EQU "true" (
	if "%backup%" EQU "true" (
		if exist "%temp%\SpotifyBackup\prefs." (
			call :spo_stp
			echo Spotify yedeÄŸi geri yÃ¼kleniyor...
			move /y "%appdata%\Spotify\prefs." "%appdata%\Spotify\prefs.backup" >nul 2>&1
			xcopy "%temp%\SpotifyBackup\prefs." "%appdata%\Spotify\" /i /y >nul 2>&1
			xcopy "%temp%\SpotifyBackup\Users\*" "%appdata%\Spotify\Users\" /s /e /i /y >nul 2>&1
			xcopy "%temp%\SpotifyBackup\Local State." "%localappdata%\Spotify\" /i /y >nul 2>&1
			if exist "%appdata%\Spotify\prefs." (
				echo [32mSpotify yedeÄŸi baÅŸarÄ±yla geri yÃ¼klendi.[0m
				rd /s /q "%temp%\SpotifyBackup" >nul 2>&1
				del /q "%appdata%\Spotify\prefs.backup" >nul 2>&1
				timeout /t %delay% /nobreak >nul 2>&1
			) else (
				echo [31mSpotify yedeÄŸi geri yÃ¼klenemedi.[0m
				move /y "%appdata%\Spotify\prefs.backup" "%appdata%\Spotify\prefs." >nul 2>&1
				if "%pause%" EQU "true" (echo Devam etmek iÃ§in herhangi bir tuÅŸa basÄ±n... & pause >nul 2>&1) else (timeout /t %delay% /nobreak >nul 2>&1)
			)
		) else (
			echo [33mSpotify yedeÄŸi bulunamadÄ±.[0m
			if "%pause%" EQU "true" (echo Devam etmek iÃ§in herhangi bir tuÅŸa basÄ±n... & pause >nul 2>&1) else (timeout /t %delay% /nobreak >nul 2>&1)
		)
	)
	if "%spotify_desktop_shortcut%" NEQ "true" (
		del /q "%userprofile%\Desktop\Spotify.lnk" >nul 2>&1
	)
)

for %%v in (
    spotify_uninstall_status
    spotify_install_status
    spotify_update_status
    spotx_uninstall_status
    spotx_install_status
    spotx_update_status
    spicetify_uninstall_status
    spicetify_install_status
    spicetify_update_status
) do (
    call :set_symbol %%v !%%v!
)

if not "%debug%" EQU "true" (cls)
echo â•”â•â•â•â•â•â•â•â•â•â•â•â•¦â•â•â•â•â•â•â•â•â•â•â•¦â•â•â•â•â•â•â•â•â•â•â•¦â•â•â•â•â•â•â•â•â•â•â•—
echo â•‘ Prog/Drum â•‘  KaldÄ±r  â•‘  YÃ¼kle   â•‘ GÃ¼ncelle â•‘
echo â• â•â•â•â•â•â•â•â•â•â•â•â•¬â•â•â•â•â•â•â•â•â•â•â•¬â•â•â•â•â•â•â•â•â•â•â•¬â•â•â•â•â•â•â•â•â•â•â•£
echo â•‘ Spotify   â•‘    !spotify_uninstall_status!    â•‘    !spotify_install_status!    â•‘    !spotify_update_status!    â•‘
echo â• â•â•â•â•â•â•â•â•â•â•â•â•¬â•â•â•â•â•â•â•â•â•â•â•¬â•â•â•â•â•â•â•â•â•â•â•¬â•â•â•â•â•â•â•â•â•â•â•£
echo â•‘ SpotX     â•‘    !spotx_uninstall_status!    â•‘    !spotx_install_status!    â•‘    !spotx_update_status!    â•‘
echo â• â•â•â•â•â•â•â•â•â•â•â•â•¬â•â•â•â•â•â•â•â•â•â•â•¬â•â•â•â•â•â•â•â•â•â•â•¬â•â•â•â•â•â•â•â•â•â•â•£
echo â•‘ Spicetify â•‘    !spicetify_uninstall_status!    â•‘    !spicetify_install_status!    â•‘    !spicetify_update_status!    â•‘
echo â•šâ•â•â•â•â•â•â•â•â•â•â•â•©â•â•â•â•â•â•â•â•â•â•â•©â•â•â•â•â•â•â•â•â•â•â•©â•â•â•â•â•â•â•â•â•â•â•
echo Ã‡Ä±kmak iÃ§in herhangi bir tuÅŸa basÄ±n... & endlocal & pause >nul 2>&1 & exit

:spo_stp
set "retry=1"
set "max_retry=3"
if not exist "%appdata%\Spotify\Spotify.exe" (
	echo [33mSpotify bulunamadÄ±, durdurulamaz.[0m
	timeout /t %delay% /nobreak >nul 2>&1
) else (
	:retry
	tasklist | findstr "Spotify.exe" >nul 2>&1
	if "%debug%" EQU "true" (echo [45;97m Debug [0m tasklist_1:!errorlevel!)
	if "!errorlevel!" EQU "1" (
		echo [33mSpotify zaten durdurulmuÅŸ.[0m
		timeout /t %delay% /nobreak >nul 2>&1
	) else (
		echo Spotify durduruluyor...
		taskkill /f /im "Spotify.exe" >nul 2>&1
		timeout /t 2 /nobreak >nul 2>&1
		tasklist | findstr "Spotify.exe" >nul 2>&1
		if "%debug%" EQU "true" (echo [45;97m Debug [0m tasklist_2:!errorlevel!)
		if "!errorlevel!" EQU "1" (
			echo [32mSpotify baÅŸarÄ±yla durduruldu.[0m
			timeout /t %delay% /nobreak >nul 2>&1
		) else (
			if "%debug%" EQU "true" (echo [45;97m Debug [0m retry:%retry%)
			if "%retry%" GEQ "%max_retry%" (
				echo Spotify %retry% kez denemenin ardÄ±ndan durdurulamadÄ±, Spotify'Ä± manuel olarak kapatmayÄ± deneyin.
				echo Devam etmek iÃ§in herhangi bir tuÅŸa basÄ±n... & pause >nul 2>&1
			)
			echo [31mSpotify durdurulamadÄ±, tekrar deneniyor...[0m
			timeout /t %delay% /nobreak >nul 2>&1
			set /a "retry+=1"
			goto :retry
		)
	)
)
exit /b

:set_symbol
set "varname=%~1"
set "value=%~2"
if "%value%" EQU "true" (
    set "%varname%=âœ”ï¸"
) else if "%value%" EQU "false" (
    set "%varname%=âŒ"
) else (
    set "%varname%=â–"
)
exit /b
